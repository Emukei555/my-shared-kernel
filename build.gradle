plugins {
    id 'java-library'
    id 'maven-publish'
    id 'java-test-fixtures'
    id 'jacoco'
}

group = 'com.yourcompany'
version = '0.2.0'

java {
    toolchain { languageVersion = JavaLanguageVersion.of(21) }
}

repositories {
    mavenCentral()
}

dependencies {
    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    // Logging
    implementation 'org.slf4j:slf4j-api:2.0.9'

    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'
    compileOnly 'org.springframework.security:spring-security-core:6.4.2'

    // OncePerRequestFilter のために必要
    implementation 'org.springframework:spring-web:6.2.1'

    // JSON
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.15.3'

    // Web / ErrorCode
    implementation 'org.springframework:spring-web:6.2.1'

    // Persistence
    compileOnly 'jakarta.persistence:jakarta.persistence-api:3.1.0'

    // Validation
    implementation 'jakarta.validation:jakarta.validation-api:3.0.2'

    // Nullability
    compileOnly 'org.jetbrains:annotations:24.1.0'

    // Spring Core
    implementation 'org.springframework:spring-tx:6.2.1'
    implementation 'org.springframework:spring-context:6.2.1'

    // Test
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.slf4j:slf4j-simple:2.0.9'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Test Fixtures
    testFixturesImplementation 'org.assertj:assertj-core:3.24.2'
    testFixturesImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testFixturesCompileOnly 'org.projectlombok:lombok:1.18.30'
    testFixturesAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Emukei555/my-shared-kernel")
            credentials {
                username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user")
                password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key")
            }
        }
    }
}

jacoco {
    toolVersion = "0.8.12"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
            excludes = [
                    'com.sqlcanvas.sharedkernel.shared.test.**',
                    'com.sqlcanvas.sharedkernel.shared.error.**'
            ]
        }
    }
}

check.dependsOn jacocoTestCoverageVerification